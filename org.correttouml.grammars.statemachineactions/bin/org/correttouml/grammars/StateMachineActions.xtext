grammar org.correttouml.grammars.StateMachineActions with org.eclipse.xtext.common.Terminals

hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate stateMachineActions "http://www.correttouml.org/grammars/StateMachineActions"

Model:
	action=Action (',' actions=Model)?;
	
Action:
	assignment=Assignment | eventAction=EventAction
;

/* 
 * This is replicated in eu.mades.assignment because I did not found a way
 * to make cross references between grammars
 */
Assignment:
	leftvar=ID '=' expression=EXPRESSION
;

EXPRESSION:
	firstTerm=TERM operator=OPERATOR secondTerm=TERM | alone=TERM
;

TERM:
	//we can decide if we want the value at the previous time instant or at the next
	(isPast='<Y>' | isFuture='<X>')? (variable=ID | constant=INT)
;

terminal OPERATOR:
	('-' | '+' | '*');

EventAction:
	//we need the link for .call events
	(link=Link)? event=Event
;

Link:
	('#' linkName=ID '.' associationEnd=ID) | self='self'
;

//Event:
//	'@' eventName=ID ('(' parameters=Parameters ')')? '.' eventExtension=EventExtensions
//;

Event:
	'@' (eventName=ID | (associationEnd=ID '.' opName=ID)) ('(' parameters=Parameters ')')? '.' eventExtension=EventExtensions 
;

Parameters :
	//TODO very bad duplication for <P> and <F> (see TERM)
	(isPast='<P>' | isFuture='<F>')? param=ID | (isPast='<P>' | isFuture='<F>')? param=ID ',' parameters=Parameters
;

EventExtensions :
	'exit' | 'enter' | 'start'  | 'end' | 'tick' | 'sig' | 'call'
;	