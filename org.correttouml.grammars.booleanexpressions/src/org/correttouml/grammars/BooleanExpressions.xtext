grammar org.correttouml.grammars.BooleanExpressions with org.eclipse.xtext.common.Terminals

hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate booleanExpressions "http://www.correttouml.org/grammars/BooleanExpressions"

Model: expression=OrExpression;

//============================================//
//				BOOLEAN EXPRESSIONS			  //
//============================================//

OrExpression:
	leftExpression=AndExpression (or=OR rightExpression=OrExpression)?
;

AndExpression:
	leftExpression=BaseExpression (and=AND rightExpression=AndExpression)?
;

BaseExpression:
	not=(NOT)? (booleanTerm=booleanTerm | '(' rootExpression=OrExpression ')')
;
	
booleanTerm:
	timeConstraint=TimeConstraint | booleanVariable=BooleanVariable | variableCondition=VariableCondition
;

TimeConstraint:
	('@' event2=Event '-' '@' event1=Event op=RELATIONS value=INT)
;

BooleanVariable:
	variable = ID
;

VariableCondition:
//	'{' expression_left=EXPRESSION relation=RELATIONS expression_right=EXPRESSION '}'
'(' expression_left=EXPRESSION relation=RELATIONS expression_right=EXPRESSION ')'
;

EXPRESSION:
	firstTerm=TERM operator=OPERATOR secondTerm=TERM | alone=TERM
;

TERM:
	variable=ID | constant=INT
;

terminal OPERATOR:
	//TODO: the minus does not work
	'+' | '*' | '--';

Event:
	(objName=ID '.')? (eventName=ID '.' eventExtension=EventExtensions) | (nowEvent ?= 'now') 
;

EventExtensions :
	'exit' | 'enter' | 'start'  | 'end' | 'tick' | 'sig' | 'call' | 'send' | 'receive' | 'reply' | 'adstart' | 'adend' | 'iodstart' | 'iodend' 
;
	
terminal RELATIONS:
	('<'| '>'| '==' | '<=' |'>=' | '!=')
;	

terminal OR: '||' ;

terminal AND: '&&';

terminal NOT: '!!';
	
